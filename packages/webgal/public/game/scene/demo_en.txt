bgm:ontama_piano1_youkoso.mp3 -volume=80 -enter=3000;
unlockBgm:ontama_piano1_youkoso.mp3 -name=welcome;
intro:*Getting started| The image materials used in this demo game are AI-generated images;
intro:*Credit| Created By ondoku3.com;
changeBg:bg.png -next;
unlockCg:bg.png -name=良い夜;
changeFigure:stand.png -left -enter=enter-from-left -next;
miniAvatar:miniavatar.png;
WebGAL:Hello -e001_Hello.mp3;
Welcome to WebGAL! -e002_Welcome_to_WebGAL.mp3;
WebGAL is a completely new web visual engine never seen before. -e003_WebGAL_is_a_completely_new_web.mp3;
changeFigure:stand2.png -right -next;
It is an engine developed using web technology, so it performs well on web pages. -e004_It_is_an_engine_developedusing_web.mp3;
Thanks to this feature, once published on your website's platform,|players can simply click a link to play your game on your website anytime, anywhere! -e005_Thanks_to_this_ feature_once.mp3;
setAnimation:move-front-and-back -target=fig-left -next;
Very attractive, don't you think? -e006_Very attractive.mp3;
changeFigure:none -right -next;
setAnimation:l2c -target=fig-left -next;
In addition, WebGAL allows you to add animations and special effects to create high quality games. -e007_In_addition_ WebGAL_allows you.mp3;
pixiInit;
pixiPerform:snow;
For example, let's make it snow as a special effect. -e008_For_example_let's_make.mp3;
As you can see, WebGAL has attractive features. |Try it out and experience it. -e009_As_you_can see_WebGAL.mp3;
That's all for the feature introduction. -e010_That's_all_for_the_feature introduction.mp3;
Next,I will introduce the history and trivia of WebGAL. -e011_Next_I_will introduce_the_ history.mp3;
pixiInit;
choose:WebGAL History:choose1|WebGAL Trivia:choose2;
;選択１
label:choose1;
WebGAL was developed to make it easier for more people to create their own visual novel games. -e012_WebGAL _was_developed to_make_it_ easier.mp3;
Initially, WebGAL had very few features, supporting only standing and background image display, audio playback, and choices. -e013_Initially_ WebGAL_had_very_few_features.mp3;
After a long development period, it has now been reborn as an engine with many excellent functions. -e014_After_a_long _development period.mp3;
Additionally, the release of the WebGAL editor makes creating and outputting games simpler and easier. -e015_Additionally,_the release_of_the WebGAL_editor.mp3;
jumpLabel:end;
;選択２
label:choose2;
The WebGAL project has reached 1000 stars on GitHub in just under a year! -e016_The_WebGAL project_has reached_1000.mp3;
The development process of WebGAL is a process of learning development while developing. -e017_The_development process_of WebGAL_is_a process.mp3;
WebGAL's scripting language was designed from the ground up to simplify production difficulty! -e018_WebGAL's _scripting language_was designed_from the_ground.mp3;
;エンド
label:end;
;changeFigure:none -left -next;
;changeFigure:stand.png -next;
The WebGAL project supports many keyboard shortcuts and the ability to go back to scenes to more closely match the functionality of a visual novel game engine for the desktop. -e019_The_WebGAL_project_supports_many_keyboard shortcuts.mp3;
Try pressing the "Backlog" button on the menu or scrolling the mouse wheel up! -e020_Try_pressing the_Backlog button.mp3;
Features like quick save, quick load, auto mode and skip mode are also supported. -e021_Features_like_quick_save_quick_load.mp3;
setTempAnimation:[{"position": {"x": 500,"y": 0},"duration": 0},{"position": {"x": 400,"y": 0},"duration": 250},{"position": {"x": 600,"y": 0},"duration": 500},{"position": {"x": 500,"y": 0},"duration": 250}] -target=fig-left -next;
For developers who are developing games for the first time, we provide beautiful general-purpose UI and rich preset functions and animations. -e022_For developers_who_ are_developing_ games.mp3;
So you can start making games quickly. -e023_So_you_can_ start_making_ games_quickly.mp3;
We hope that your work will be exhibited at WebGAL. -e024_We_hope_ that_your_work.mp3;
Thank you for your interest in the WebGAL project! -e025_Thank_you_ for_your_interest_in_the_WebGAL_project!.mp3;
end;