bgm:ontama_piano1_youkoso.mp3 -volume=80 -enter=3000;
unlockBgm:ontama_piano1_youkoso.mp3 -name=ようこそ;
intro:■初めに| このデモゲームで使用している画像素材は、AI生成画像です;
intro:■クレジット| 「VOICEVOX:小夜/SAYO」;
changeBg:bg.png -next;
unlockCg:bg.png -name=良い夜; // CGのロックを解いて、名前をつけます。
changeFigure:stand.png -left -enter=enter-from-left -next;
miniAvatar:miniavatar.png;
WebGAL:こんにちは。 -001_小夜SAYO（ノーマル）_こんにちは。.wav;
WebGALへようこそ！ -002_小夜SAYO（ノーマル）_ウェブギャルへよう….wav;
WebGALは、かつてないまったく新しいビジュアルノベルエンジンです。 -003_小夜SAYO（ノーマル）_ウェブギャルは、か….wav;
changeFigure:stand2.png -right -next;
Web技術を使用して開発されたエンジンであるため、ウェブページで優れたパフォーマンスを発揮します。 -004_小夜SAYO（ノーマル）_Web技術を使用し….wav;
この機能のおかげで、ウェブサイト上に公開すると、プレイヤーはリンクを押すだけで、 -005_小夜SAYO（ノーマル）_この機能のおかげで….wav;
いつでも、どこでも、ウェブサイト上でゲームをプレイすることができます！ -006_小夜SAYO（ノーマル）_いつでも、どこでも….wav;
setAnimation:move-front-and-back -target=fig-left -next;
とっても、魅力的ではありませんか？ -007_小夜SAYO（ノーマル）_とっても、魅力的で….wav;
changeFigure:none -right -next;
setAnimation:l2c -target=fig-left -next;
さらに、WebGALでは、アニメーションや特殊効果を追加することができ、クオリティの高いゲームを作成することができます。 -008_小夜SAYO（ノーマル）_さらに、ウェブギャ….wav;
pixiInit;
pixiPerform:snow;
例えば、特殊効果として、雪を降らせてみます。 -009_小夜SAYO（ノーマル）_例えば、特殊効果と….wav;
こんな感じに、WebGALには、魅力的な機能があります。| ぜひ、使ってみて体験してみてください。 -010_小夜SAYO（ノーマル）_こんな感じに、ウェ….wav;
機能の紹介は以上です。 -011_小夜SAYO（ノーマル）_機能の紹介は以上で….wav;
次に、WebGALの歴史と豆知識について紹介します。 -012_小夜SAYO（ノーマル）_次に、ウェブギャル….wav;
興味があれば、引き続きお聞きください。 -013_小夜SAYO（ノーマル）_興味があれば、引き….wav;
pixiInit;
choose:WebGAL 歴史:choose1|WebGAL 豆知識:choose2;

;選択１
label:choose1;
WebGALは、より多くの人が自分のビジュアルノベルゲームを簡単に作成できるようにと、開発されました。 -014_小夜SAYO（ノーマル）_ウェブギャルは、よ….wav;
当初のWebGALには、機能がほとんどなく、立ち絵と背景画像の表示、音声再生、選択肢の選択機能のみをサポートしていました。 -015_小夜SAYO（ノーマル）_当初のウェブギャル….wav;
長きにわたる開発期間を経て、今では多くの優れた機能があるエンジンとして、生まれ変わりました。  -016_小夜SAYO（ノーマル）_長きにわたる開発期….wav;
さらに、WebGALエディターのリリースにより、ゲームの作成と出力がよりシンプルかつ容易になりました。 -017_小夜SAYO（ノーマル）_さらに、ウェブギャ….wav;
jumpLabel:end;

;選択２
label:choose2;
WebGALプロジェクトは、わずか1年足らずでGitHubで1000スターを獲得しました！ -019_小夜SAYO（ノーマル）_ウェブギャルプロジ….wav;
WebGALの開発過程は、開発しながら開発を学ぶプロセスです。 -020_小夜SAYO（ノーマル）_ウェブギャルの開発….wav;
そのため、彼女は3回のリファクタリングを経験し、毎回ほぼゼロからやり直す形となりました。 -021_小夜SAYO（ノーマル）_そのため、彼女は3….wav;
WebGALのスクリプト言語は、制作の難易度を簡素化するためにゼロから設計されました！ -022_小夜SAYO（ノーマル）_ウェブギャルのスク….wav;
;
;l2d
;label:l2d;
;changeFigure:hiyori/hiyori_pro_t11.model3.json -left -motion=Idle -next;
;WebGALプロジェクトでは、現在Live2Dの実験的なサポートも始まっています！これからは正式な機能として成長する可能性もあります！ -023_小夜SAYO（ノーマル）_ウェブギャルプロジ….wav
;
;エンド
label:end;
;changeFigure:none -left -next;
;changeFigure:stand.png -next;
WebGALプロジェクトは、デスクトップ向けのビジュアルノベルゲームエンジンの機能により近づくため、多くのショートカットキーとシーンの戻る機能をサポートしています。 -024_小夜SAYO（ノーマル）_ウェブギャルプロジ….wav;
メニューの「LOG」ボタンを押すか、マウスホイールを上にスクロールしてみてください！ -025_小夜SAYO（ノーマル）_メニューのログボタ….wav;
豊富な設定、クイックセーブ、クイックロード、自動モード、スキップモードなどの機能もサポートされています。 -026_小夜SAYO（ノーマル）_豊富な設定、クイッ….wav;
setTempAnimation:[{"position": {"x": 500,"y": 0},"duration": 0},{"position": {"x": 400,"y": 0},"duration": 250},{"position": {"x": 600,"y": 0},"duration": 500},{"position": {"x": 500,"y": 0},"duration": 250}] -target=fig-left -next;
初めてゲームを開発する開発者のために、美しい汎用UIと豊富なプリセット機能やアニメーションを提供しています。 -028_小夜SAYO（ノーマル）_初めてゲームを開発….wav
ですから、心配せずに素早くゲーム制作を始めることができます。 -029_小夜SAYO（ノーマル）_ですから、心配せず….wav;
WebGALプロジェクトチームは、あなたの作品がWebGALで展示されることを期待しています！ -030_小夜SAYO（ノーマル）_ウェブギャルプロジ….wav
リンクを1つ用意するだけで、無数のユーザーがあなたの作品をすぐに楽しめるようになります。 -031_小夜SAYO（ノーマル）_リンクを1つ用意す….wav;
WebGALプロジェクトへのご注目、ありがとうございます！ -032_小夜SAYO（ノーマル）_ウェブギャルプロジ….wav;
end;